buildscript {
    ext.spring_boot_version = '2.0.2.RELEASE'
    ext.spring_boot_gradle_plugin_version = '2.0.2.RELEASE'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version")
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/exposed' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
}

apply plugin: 'kotlin'
apply plugin: 'org.springframework.boot'

dependencies {
    compile("org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version")
    compile project(":webapp-common")
    compile project(":cordapp-common")
    compile project(":cordapp-contracts-states")
    compile project(":cordapp-buyer")
}

configurations {
    //remove default logger
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        apiVersion = "1.3"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}

task runBuyerServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.server.ServerKt'
    environment "config.rpc.username", "user1"
    environment "config.rpc.password", "test"
    environment "config.rpc.host", "localhost"
    environment "config.rpc.port", "10009"
    environment "server.port", "10109"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'net.corda.server.ServerKt'
        )
    }
}