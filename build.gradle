buildscript {
    ext.corda_release_group = 'net.corda'
    ext.corda_release_version = '4.3'
    ext.corda_gradle_plugins_version = '4.0.42'
    ext.kotlin_version = '1.3.21'
    ext.junit_version = '4.12'
    ext.quasar_version = '0.7.10'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/exposed' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
}

apply plugin: 'kotlin'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'
apply from:   'commons.gradle'

dependencies {
    cordapp project(":cordapp-buyer")
    cordapp project(":cordapp-seller")
    cordapp project(":cordapp-contracts-states")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        apiVersion = "1.3"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    directory "./build/nodes"
    node {
        name "O=Notary,L=London,C=GB"
        notary = [validating : true]
        p2pPort 10002
        rpcSettings {
            address("localhost:10003")
            adminAddress("localhost:10043")
        }
        cordapps = [
                "$project.group:cordapp-buyer:$project.version",
                "$project.group:cordapp-seller:$project.version",
                "$project.group:cordapp-contracts-states:$project.version"
        ]
    }
    node {
        name "O=Seller,L=London,C=GB"
        p2pPort 10005
        rpcSettings {
            address("localhost:10006")
            adminAddress("localhost:10046")
        }
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
        cordapps = [
                "$project.group:cordapp-buyer:$project.version",
                "$project.group:cordapp-seller:$project.version",
                "$project.group:cordapp-contracts-states:$project.version"
        ]
    }
    node {
        name "O=Buyer,L=London,C=GB"
        p2pPort 10008
        rpcSettings {
            address("localhost:10009")
            adminAddress("localhost:10049")
        }
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
        cordapps = [
                "$project.group:cordapp-buyer:$project.version",
                "$project.group:cordapp-seller:$project.version",
                "$project.group:cordapp-contracts-states:$project.version"
        ]
    }
}



